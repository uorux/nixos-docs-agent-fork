# Post-unlock PCR 15 verification for TPM2 LUKS auto-unlock.
# Defense against filesystem-confusion attacks (oddlama, 2025-01-16).
# Per-host opt-in via modules.system.pcr-verification.enable.
#
# See modules/system/PCR-VERIFICATION.md for the operator runbook.
{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.modules.system.pcr-verification;
in
{
  options.modules.system.pcr-verification = {
    enable = lib.mkEnableOption "PCR 15 verification for TPM2 LUKS unlock";

    deviceName = lib.mkOption {
      type = lib.types.str;
      default = "luks";
      description = ''
        Name of the boot.initrd.luks.devices entry to attach TPM2 measurement to.
        Defaults to the repo-wide "luks" convention; override on hosts that use a
        unique mapper name to avoid a mint-time /dev/mapper collision (e.g.
        arquitens uses "cryptarquitens").
      '';
    };

    expectedPcr15 = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = null;
      description = ''
        Expected SHA-256 hex of TPM PCR 15 after all LUKS volumes have been
        unlocked and their keys measured. Capture with
        `systemd-analyze pcrs 15 --json=short` on a known-good boot.
        When null, only measurement is enabled (no enforcement) - use this
        for the bootstrap boot, then fill in the captured value.
      '';
      example = "caf33e79c645b65849256238a11fa68ae197e5cb89730c463c1cdf1d9128376f";
    };
  };

  config = lib.mkIf cfg.enable {
    # Most hosts name their LUKS device "luks" (cfg.deviceName default); hosts
    # minted from a running box use a unique name to dodge the /dev/mapper/luks
    # collision and set cfg.deviceName accordingly. We can't `mapAttrs` over
    # `config.boot.initrd.luks.devices` here - that's infinite recursion (reading
    # the attr we're writing) - so the name is threaded through explicitly.
    assertions = [
      {
        assertion = builtins.hasAttr cfg.deviceName config.boot.initrd.luks.devices;
        message = ''
          modules.system.pcr-verification.deviceName = "${cfg.deviceName}" but no
          such LUKS device is defined (check the host's disko `name`).
        '';
      }
    ];
    boot.initrd = {
      # tpm2-device=auto must accompany tpm2-measure-pcr=yes. Setting
      # the measure option flips systemd-cryptsetup from the
      # libcryptsetup-plugin auto-detect path to the "respect crypttab
      # options literally" path. In that mode, TPM2 unseal is only
      # attempted if tpm2-device is also declared - without it the
      # service silently falls back to passphrase, even with a valid
      # TPM2 token in the LUKS header. See systemd issue #37072.
      luks.devices.${cfg.deviceName}.crypttabExtraOpts = [
        "tpm2-device=auto"
        "tpm2-measure-pcr=yes"
      ];

      systemd = {
        # Initrd-stage emergency shell uses the root password, so a PCR
        # mismatch (or any other initrd failure) lands at an
        # authenticated rescue prompt instead of an unbootable system.
        # Upstream default is `false` (no rescue access at all).
        emergencyAccess = config.users.users.root.hashedPassword;

        # jq is needed inside the initrd to parse `systemd-analyze pcrs`.
        storePaths = [ "${pkgs.jq}/bin/jq" ];

        services.check-pcr15 = lib.mkIf (cfg.expectedPcr15 != null) {
          description = "Verify TPM PCR 15 matches expected post-unlock value";
          wantedBy = [ "initrd.target" ];
          requiredBy = [ "sysroot.mount" ];
          after = [ "cryptsetup.target" ];
          # Run before the impermanence rollback so a mismatched boot never
          # touches the (potentially attacker-controlled) btrfs.
          before = [
            "sysroot.mount"
            "rollback.service"
          ];
          unitConfig.DefaultDependencies = "no";
          serviceConfig = {
            Type = "oneshot";
            RemainAfterExit = true;
          };
          # Must be the initrd's own systemd (the only one copied into the
          # initrd): make-initrd-ng follows ELF deps, not script references,
          # and pkgs.systemd is no longer the same derivation as
          # config.systemd.package (nixpkgs patches the latter via `apply`).
          script = ''
            actual=$(${config.boot.initrd.systemd.package}/bin/systemd-analyze pcrs 15 --json=short \
                     | ${pkgs.jq}/bin/jq -r '.[0].sha256')
            if [[ "$actual" != "${cfg.expectedPcr15}" ]]; then
              echo "PCR 15 verification FAILED" >&2
              echo "  expected: ${cfg.expectedPcr15}" >&2
              echo "  actual:   $actual" >&2
              exit 1
            fi
            echo "PCR 15 verification OK ($actual)"
          '';
        };
      };
    };
  };
}
